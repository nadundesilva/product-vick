@App:name("telemetry-app")
@App:description("Cellery Telemetry app for processing and storing http requests")

@source(type="inMemory", topic="istio-mixer-report", @map(type="passThrough"))
define stream TelemetryStreamIn(sourceNamespace string, sourceCell string, sourceComponent string,
                                sourcePod string, destinationNamespace string, destinationCell string,
                                destinationComponent string, destinationPod string, contextReporterKind string,
                                requestHeadersMap object, requestPath string, requestMethod string,
                                requestSizeBytes long, responseCode long, responseDurationNanoSec int,
                                responseSizeBytes long);

define stream ProcessedRequestsStream(sourceCell string, sourceComponent string, destinationCell string,
                                      destinationComponent string, httpResponseGroup string,
                                      responseTimeMilliSec double, requestSizeBytes long, responseSizeBytes long);

@store(type="rdbms", datasource="CELLERY_OBSERVABILITY_DB", field.length="sourceCell:253, sourceComponent:253, destinationCell:253, destinationComponent: 253, httpResponseGroup:3")
@purge(enable="false")
define aggregation RequestAggregation from ProcessedRequestsStream
select
    sourceCell,
    sourceComponent,
    destinationCell,
    destinationComponent,
    httpResponseGroup,
    sum(responseTimeMilliSec) as totalResponseTimeMilliSec,
    sum(requestSizeBytes) as totalRequestSizeBytes,
    sum(responseSizeBytes) as totalResponseSizeBytes,
    count() as requestCount
group by
    sourceCell, sourceComponent, destinationCell, destinationComponent, httpResponseGroup
aggregate every sec...year;

--
-- Storing the data received from Istio Telemetry
--

from TelemetryStreamIn
select
    sourceCell,
    sourceComponent,
    destinationCell,
    destinationComponent,
    contextReporterKind,
    cast(map:get(requestHeadersMap, "x-request-id"), "string") as requestId,
    str:concat(convert(cast(responseCode, "int") / 100, "string"), "xx") as httpResponseGroup,
    convert(responseDurationNanoSec, "double") / 1000000 as responseTimeMilliSec,
    ifThenElse(requestSizeBytes is null, 0L, requestSizeBytes) as requestSizeBytes,
    ifThenElse(responseSizeBytes is null, 0L, responseSizeBytes) as responseSizeBytes
insert into ProcessedTelemetryStream;

from every(e1=ProcessedTelemetryStream) ->
    not ProcessedTelemetryStream[e1.requestId == e2.requestId] for 1 minute
    or e2=ProcessedTelemetryStream[e1.requestId == e2.requestId] within 1 minute
select
    e1.sourceCell,
    e1.sourceComponent,
    e1.destinationCell,
    e1.destinationComponent,
    ifThenElse(e1.httpResponseGroup is null, e2.httpResponseGroup, e1.httpResponseGroup) as httpResponseGroup,
    ifThenElse(e1.responseTimeMilliSec is null, e2.responseTimeMilliSec, e1.responseTimeMilliSec) as responseTimeMilliSec,
    ifThenElse(e1.requestSizeBytes is null, e2.requestSizeBytes, e1.requestSizeBytes) as requestSizeBytes,
    ifThenElse(e1.responseSizeBytes is null, e2.responseSizeBytes, e1.responseSizeBytes) as responseSizeBytes
insert into SanitizedRequestsStream;

from SanitizedRequestsStream[(not httpResponseGroup is null) and (not responseTimeMilliSec is null)
                             and (not requestSizeBytes is null) and (not responseSizeBytes is null)]
insert into ProcessedRequestsStream;
